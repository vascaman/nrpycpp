# obtained this CMakeLists.txt: modifying this : https://code.qt.io/cgit/pyside/pyside-setup.git/tree/examples/samplebinding/CMakeLists.txt
# reference for type converters: https://pyside.github.io/docs/shiboken/typeconverters.html
# launch: 
#		$ cmake -H.. -B. -G "Unix Makefiles" -DCMAKE_BUILD_TYPE=Release
#		$ make


cmake_minimum_required(VERSION 3.16)
cmake_policy(VERSION 3.16)

# Enable policy to not use RPATH settings for install_name on macOS.
if(POLICY CMP0068)
  cmake_policy(SET CMP0068 NEW)
endif()

#checking python
if(NOT python_interpreter)
    find_program(python_interpreter "python3")
endif()

# Macro to get various pyside / python include / link flags and paths.
# Uses the not entirely supported utils/pyside_config.py file.
macro(pyside_config option output_var)
    if(${ARGC} GREATER 2)
        set(is_list ${ARGV2})
    else()
        set(is_list "")
    endif()

    execute_process(
      COMMAND ${python_interpreter} "${CMAKE_SOURCE_DIR}/pyside_config.py"
              ${option}
      OUTPUT_VARIABLE ${output_var}
      OUTPUT_STRIP_TRAILING_WHITESPACE)

    if ("${${output_var}}" STREQUAL "")
        message(FATAL_ERROR "Error: Calling pyside_config.py ${option} returned no output.")
    endif()
    if(is_list)
        string (REPLACE " " ";" ${output_var} "${${output_var}}")
    endif()
endmacro()

pyside_config(--python-interpeter python_interpreter)
message(STATUS "Using python interpreter: ${python_interpreter}")
# Setting project name
project(NrPyCppCallbacks)

# Set CPP standard to C++17 minimum.
set(CMAKE_CXX_STANDARD 17)

# The name of the generated bindings module (as imported in Python).
pyside_config(--lib-name bindings_library)

#WRAPPED LIB PATHS
pyside_config(--lib-include-dir wrapped_lib_include_dir)
pyside_config(--lib-dir wrapped_lib_dir)
pyside_config(--lib-bin wrapped_lib_bin)

#BINDINGS
pyside_config(--pyside-header header_file)
pyside_config(--pyside-typesystem-file typesyste_file)


# The header file with all the types and functions for which bindings will be generated.
# Usually it simply includes other headers of the library you are creating bindings for.
set(wrapped_header ${CMAKE_SOURCE_DIR}/${header_file})

# The typesystem xml file which defines the relationships between the C++ types / functions
# and the corresponding Python equivalents.
set(typesystem_file ${CMAKE_SOURCE_DIR}/${typesyste_file})


# Specify which C++ files will be generated by shiboken. This includes the module wrapper
# and a '.cpp' file per C++ type. These are needed for generating the module shared
# library.
set(generated_sources
    ${CMAKE_CURRENT_BINARY_DIR}/${bindings_library}/nrpycallbacks_module_wrapper.cpp
    ${CMAKE_CURRENT_BINARY_DIR}/${bindings_library}/pycallback_wrapper.cpp
    )

find_package(Qt6 COMPONENTS Core REQUIRED)



#set(shiboken_module_path /usr/lib/python3/dist-packages/shiboken2)
#message(STATUS "shiboken_module_path :${shiboken_module_path}")

pyside_config(--python-include-dir python_include_dir)
message(STATUS "python_include_dir :${python_include_dir}")

pyside_config(--shiboken-include-dir shiboken_include_dir)
message(STATUS "shiboken_include_dir :${shiboken_include_dir}")

pyside_config(--shiboken-shared-lib  shiboken_shared_libraries)
message(STATUS "shiboken_shared_libraries :${shiboken_shared_libraries}")


#setting up shiboken
#set(shiboken_generator_path /usr/bin)
pyside_config(--shiboken-generator-path  shiboken_generator_path)
message(STATUS "shiboken_generator_path :${shiboken_generator_path}")

pyside_config(--shiboken-bin  shiboken_generator_bin)

set(shiboken_path "${shiboken_generator_path}/${shiboken_generator_bin}${CMAKE_EXECUTABLE_SUFFIX}")

if(NOT EXISTS ${shiboken_path})
    message(FATAL_ERROR "Shiboken executable not found at path: ${shiboken_path}")
endif()
message(STATUS "shiboken_path: ${shiboken_path}")

# =========================== PySide Configuration SubSection Start ===========================

#needed only if you use pyside for bind signal/slot

pyside_config(--pyside-typesystems-path pyside_typesystems_path)
message(STATUS "pyside_typesystems_path :${pyside_typesystems_path}")

pyside_config(--pyside-include-dir pyside_include_dir)
message(STATUS "pyside_include_dir :${pyside_include_dir}")

pyside_config(--pyside-shared-lib pyside_shared_libraries)
message(STATUS "pyside_shared_libraries :${pyside_shared_libraries}")

#set(pyside_module_path /usr/lib/python3/dist-packages/shiboken2)
#message(STATUS "pyside_module_path :${pyside_module_path}")

# ============================ PySide Configuration SubSection End ============================

# ====================== Shiboken target for generating binding C++ files  ====================

pyside_config(--qt-include-dir Qt6Core_include_dirs)
pyside_config(--qt-lib-dir Qt6Core_lib_dirs)

set(shiboken_include_option ${Qt6Core_include_dirs}:${Qt6Core_include_dirs}/QtCore:${Qt6Core_include_dirs}/QtWidgets:${shiboken_include_dir}:${pyside_include_dir}:${pyside_include_dir}/QtCore:${pyside_include_dir}/QtWidgets:${pyside_include_dir}/QtGui:${CMAKE_SOURCE_DIR}:${CMAKE_CURRENT_BINARY_DIR}/${bindings_library}:${wrapped_lib_include_dir})

message(STATUS "includes: ${shiboken_include_option}")

# Set up the options to pass to shiboken.
set(shiboken_options 
    --generator-set=shiboken 
    --enable-parent-ctor-heuristic 
    #--skip-deprecated
    --enable-return-value-heuristic 
    --use-isnull-as-nb_nonzero
    --avoid-protected-hack 
    #--language-level=c++11
    --enable-pyside-extensions
    --include-paths=${shiboken_include_option}
    --typesystem-paths=${CMAKE_SOURCE_DIR}:${pyside_typesystems_path}
    --output-directory=${CMAKE_CURRENT_BINARY_DIR}
    )
    
set(generated_sources_dependencies ${wrapped_header} ${typesystem_file})

# Add custom target to run shiboken to generate the binding cpp files.
add_custom_command(OUTPUT ${generated_sources}
                    COMMAND ${shiboken_path}
                    ${shiboken_options} ${wrapped_header} ${typesystem_file}
                    DEPENDS ${generated_sources_dependencies}
                    IMPLICIT_DEPENDS CXX ${wrapped_header}
                    WORKING_DIRECTORY ${CMAKE_CURRENT_BINARY_DIR}
                    COMMENT "Running generator for ${typesystem_file}.")
                    
# =============================== CMake target - bindings_library =============================

# Set the cpp files which will be used for the bindings library.
set(${bindings_library}_sources ${generated_sources})

# Define and build the bindings library.
add_library(${bindings_library} MODULE ${${bindings_library}_sources})


target_include_directories(${bindings_library} PRIVATE ${wrapped_lib_include_dir})
target_link_libraries(${bindings_library} PRIVATE ${wrapped_lib_dir}/${wrapped_lib_bin})

target_include_directories(${bindings_library} PRIVATE ${python_include_dir})
target_include_directories(${bindings_library} PRIVATE ${shiboken_include_dir})
target_include_directories(${bindings_library} PRIVATE ${pyside_include_dir})
target_include_directories(${bindings_library} PRIVATE ${pyside_include_dir}/QtCore)
target_include_directories(${bindings_library} PRIVATE ${pyside_include_dir}/QtGui)
target_link_libraries(${bindings_library} PRIVATE ${shiboken_shared_libraries})
target_link_libraries(${bindings_library} PRIVATE ${pyside_shared_libraries})
target_include_directories(${bindings_library} PRIVATE ${CMAKE_SOURCE_DIR})

#include_directories(${Qt6Core_INCLUDE_DIRS})
target_include_directories(${bindings_library} PRIVATE ${Qt6Core_include_dirs})
target_include_directories(${bindings_library} PRIVATE ${Qt6Core_include_dirs}/QtCore)
target_include_directories(${bindings_library} PRIVATE ${Qt6Core_include_dirs}/QtGui)
target_link_libraries(${bindings_library} PRIVATE ${Qt6Core_lib_dirs}/libQt6Core.so)
target_link_libraries(${bindings_library} PRIVATE ${Qt6Core_lib_dirs}/libQt6Gui.so)

# Adjust the name of generated module.
set_property(TARGET ${bindings_library} PROPERTY PREFIX "")
set_property(TARGET ${bindings_library} PROPERTY OUTPUT_NAME
             "${bindings_library}${PYTHON_EXTENSION_SUFFIX}")



